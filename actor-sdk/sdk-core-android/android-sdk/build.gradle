apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'im.actor'
version = '0.0.1'

def baseVersion = "3.0"

android {

    compileSdkVersion 27
    buildToolsVersion '27.0.2'

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        minSdkVersion 15
        targetSdkVersion 25
        renderscriptSupportModeEnabled true

        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK:=jni/Application.mk", "APP_PLATFORM:=android-14"
                abiFilters "armeabi-v7a", "x86", "armeabi"
            }
        }
    }

    sourceSets {
        main {

            res {
                srcDirs = ['src/main/res',
                           'src/main/res-material']
            }

            resources {
                srcDirs = ['../../sdk-core/core/core-shared/src/main/resources']
            }

            assets {
                srcDirs = ['../../sdk-core/core/core-shared/src/main/resources',
                           'src/main/assets']
            }

            java {
                srcDirs = ['src/main/java',
                           '../../sdk-core/core/core-shared/src/main/java',
                           '../../sdk-core/core/core-android/src/main/java',
                           '../../sdk-core/runtime/runtime-shared/src/main/java',
                           '../../sdk-core/runtime/runtime-generic/src/main/java',
                           '../../sdk-core/runtime/runtime-android/src/main/java']
            }
        }
    }

    sourceSets.main.jniLibs.srcDirs = ['./jni/','./src/main/jniLibs/']

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Calls
    api 'io.pristine:libjingle:11139@aar'

    // UI
    api 'com.facebook.fresco:fresco:1.5.0'
    api 'com.facebook.fresco:animated-gif:1.5.0'
    api 'com.facebook.fresco:webpsupport:1.5.0' // Not Enabling animated WebP yet
    api 'com.github.castorflex.smoothprogressbar:library-circular:1.2.0'
    api 'com.facebook.rebound:rebound:0.3.8'
    api 'com.rengwuxian.materialedittext:library:2.1.4'

    api 'com.soundcloud.android:android-crop:1.0.1@aar'
    api 'com.github.chrisbanes:PhotoView:2.1.3'

    api 'com.droidkit:progress:0.5'

    // Phone parser
    api 'io.michaelrocks:libphonenumber-android:8.8.5'

    // Core
    api 'com.squareup.okhttp:okhttp:2.5.0'

    // Android Support
    api 'com.android.support:support-v4:27.0.2'
    api 'com.android.support:support-v13:27.0.2'
    api 'com.android.support:appcompat-v7:27.0.2'
    api 'com.android.support:palette-v7:27.0.2'
    api 'com.android.support:recyclerview-v7:27.0.2'
    api 'com.android.support:design:27.0.2'
    api 'com.android.support:customtabs:27.0.2'
    api 'com.android.support.constraint:constraint-layout:1.0.2'

    api 'com.googlecode.mp4parser:isoparser:1.0.6'
}

String ossrhUsername = ""
String ossrhPassword = ""
boolean isDeployConfigured = false
if (project.rootProject.file('gradle.properties').exists()) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
    ossrhUsername = properties.getProperty("ossrhUsername", "")
    ossrhPassword = properties.getProperty("ossrhPassword", "")
    version = baseVersion + "." + properties.getProperty("build_index", "")

    print("##teamcity[buildNumber '$version']")

    nexusStaging {
        username ossrhUsername
        password ossrhPassword
    }

    isDeployConfigured = true
}

project.afterEvaluate {

    if (isDeployConfigured) {

        uploadArchives {
            repositories {
                mavenDeployer {
                    repository(url: "http://nexus.diegosilva.com.br:8081/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'Actor SDK for Android'
                        packaging 'aar'
                        description 'Actor SDK: Embedding mobile messaging to Android App'
                        url 'https://github.com/actorapp/actor-platform'

                        scm {
                            url 'scm:git@github.com:actorapp/actor-platform.git'
                            connection 'scm:git@github.com:actorapp/actor-platform.git'
                            developerConnection 'scm:git@github.com:actorapp/actor-platform.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'ex3ndr'
                                name 'Stepan Korshakov'
                            }
                            developer {
                                id 'dfsilva'
                                name 'Diego Silva'
                            }
                        }
                    }
                }
            }
        }
    }
}